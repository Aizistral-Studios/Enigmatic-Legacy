buildscript {
    repositories {
        maven {
            name "Forge"
            url "https://files.minecraftforge.net/maven"
        }
        maven {
            name "Sponge"
            url "https://repo.spongepowered.org/repository/maven-public/"
        }
        maven { url 'https://plugins.gradle.org/m2/' }
        jcenter()
        mavenCentral()
    }
    dependencies {
        classpath group: 'net.minecraftforge.gradle', name: 'ForgeGradle', version: '3.+'
		classpath 'gradle.plugin.com.matthewprenger:CurseGradle:1.4.0'
        classpath group: 'org.spongepowered', name: 'mixingradle', version: '0.7-SNAPSHOT'
    }
}

println("Applying plugins...")

apply plugin: 'net.minecraftforge.gradle'
apply plugin: 'eclipse'
apply plugin: 'maven-publish'
apply plugin: 'org.spongepowered.mixin'
apply plugin: 'com.matthewprenger.cursegradle'

mixin {
	println("Specifying refmaps for Mixin...")
	
    add sourceSets.main, "enigmaticlegacy.refmap.json"
}

println("Specifying project version/group/archivesBaseName...")
version = "${mod_version}"
group = "${mod_group}"
archivesBaseName = "${mod_archives_name}"


println("Specifying Java compatibility...")
sourceCompatibility = targetCompatibility = compileJava.sourceCompatibility = compileJava.targetCompatibility = '1.8'

println("Java Installation Data:")
println(" - Java Version: " + System.getProperty('java.version'))
println(" - JVM Version: " + System.getProperty('java.vm.version'))
println(" - Vendor: " + System.getProperty('java.vendor'))
println(" - OS Archetype: " + System.getProperty('os.arch'))

minecraft {
	println("Specifying MCP mappings...")
    mappings channel: 'snapshot', version: "${version_mcp}"
	println("Specifying Access Transformer configuration...")	
	accessTransformer = file('src/main/resources/META-INF/accesstransformer.cfg')
	
	runs {
        client {
			properties 'org.gradle.jvmargs': '-Xms4G -Xmx4G'
			properties 'fml.earlyprogresswindow': 'false'
			properties 'mixin.env.disableRefMap': 'true'
            workingDirectory project.file('run')

            property 'forge.logging.markers', 'SCAN,REGISTRIES,REGISTRYDUMP'
            property 'forge.logging.console.level', 'debug'

            mods {
                enigmaticlegacy {
                    source sourceSets.main
                }
            }
        }

        server {
			properties 'mixin.env.disableRefMap': 'true'
            workingDirectory project.file('run')

            property 'forge.logging.markers', 'SCAN,REGISTRIES,REGISTRYDUMP'
            property 'forge.logging.console.level', 'debug'

            mods {
                enigmaticlegacy {
                    source sourceSets.main
                }
            }
        }
    }
}

sourceSets.main.resources {
    // NO-OP
}

repositories {
	println("Specifying repositories...")
	
	
	flatDir {  dirs 'deps' }
	maven {  url = "https://maven.theillusivec4.top/" } // curios
	maven {  url = "https://maven.blamejared.com/" } // patchouli
	maven {  url = "https://repo.spongepowered.org/maven" } // mixin
    maven {	 url "https://dvs1.progwml6.com/files/maven/" } // no idea
}

dependencies {
	println("Processing dependencies...")	
	
	
	// Minecraft & Forge
    minecraft "net.minecraftforge:forge:${version_forge}"
   
	// Mixin
    annotationProcessor 'org.spongepowered:mixin:0.8'
    annotationProcessor 'com.google.code.gson:gson:2.8.0'
    annotationProcessor 'com.google.guava:guava:21.0'
    annotationProcessor 'org.ow2.asm:asm-tree:7.2'
    annotationProcessor 'org.ow2.asm:asm-commons:7.2'
    annotationProcessor 'org.ow2.asm:asm-util:7.2'
	
	// These are 1.16.3 - 1.16.4 dependencies
	compile fg.deobf('generic:decorativeblocks:1.16.1-1.5.2')
	compile fg.deobf('generic:WorldNameRandomizer:FORGE-1.16.4-v1.1.1')
	compile fg.deobf('generic:BuildResourceUpdater:1.0')
	compile fg.deobf('top.theillusivec4.curios:curios-forge:1.16.3-4.0.2.0')
    compile fg.deobf("vazkii.patchouli:Patchouli:1.16.2-47")
	
	//compile fg.deobf('generic:Survive:1.16.3-3.0.2')
	//compile fg.deobf('generic:EnchantmentDescriptions:1.16.4-6.0.1')
    //compile fg.deobf("generic:Apotheosis:1.16.3-4.4.1")
    //compile fg.deobf("generic:Placebo:1.16.3-4.3.3")
	
	// These are 1.16.2 dependencies
	//compile fg.deobf('top.theillusivec4.curios:curios-forge:1.16.2-4.0.0.1')
    //compile fg.deobf("vazkii.patchouli:Patchouli:1.16-41")
	
	// These are 1.16.1 dependencies
	//compile fg.deobf('generic:decorativeblocks:1.16.1-1.4')
	//compile fg.deobf('top.theillusivec4.curios:curios:FORGE-1.16.1-3.0.0.2')
    //compile fg.deobf("vazkii.patchouli:Patchouli:1.16-39")

}


jar {
	doFirst {
		println("Processing manifest...")
	}		

    manifest {
        attributes([
            "Specification-Title": "${mod_name}",
            "Specification-Vendor": "${mod_author}",
            "Specification-Version": "${mod_version}",
            "Implementation-Title": "${mod_name}",
            "Implementation-Version": "${mod_version}",
            "Implementation-Vendor" :"${mod_author}",
            "Implementation-Timestamp": new Date().format("yyyy-MM-dd'T'HH:mm:ssZ"),
            "MixinConnector": "${project.group}.MixinConnector"
        ])
    }
}

processResources {
	doFirst {
		println("Processing resources...")
		println("Specifying The Acknowledgment version in book.json...")	
	}
	
	filesMatching('data/enigmaticlegacy/patchouli_books/the_acknowledgment/book.json') {
        filter {
			// Bold of you to assume I know how to inflate with Gradle
            it.replaceAll("~the_acknowledgment_edition", findProperty('the_acknowledgment_edition') ?: 'invalid_edition')
        }
    }

    from("${project.projectDir}/docs") {
		println("Inflating mods.toml...")	
	
		// Replace properties in mods.toml with ones derived from settings.gradle
        include 'META-INF/mods.toml'
        expand 'mod_license': mod_license, 
		'mod_version': project.version, 
		'mod_id' : mod_id, 
		'mod_name': mod_name, 
		'mod_url': mod_url, 
		'mod_author': mod_author, 
		'mod_description': mod_description, 
		'mod_icon': mod_icon, 
		'issue_tracker_url': issue_tracker_url, 
		'mod_credits': mod_credits, 
		'dep_forge': dep_forge, 
		'dep_minecraft': dep_minecraft, 
		'dep_patchouli': dep_patchouli, 
		'dep_curios': dep_curios
    }

    from(sourceSets.main.resources.srcDirs) {
        exclude 'META-INF/mods.toml'
    }

    from("${project.projectDir}/docs") {
		println("Including documentation...")	
		// Include a license and changelog into build .jar
        include 'changelog.html'
        include 'LICENSE'
    }
}

curseforge {

    project {
		println("Pushing CurseGradle specifications for project ID: ${curse_id}")
		
		// Specified as cmd argument
        apiKey = findProperty('curseKey') ?: 0
        
		id = "${curse_id}"
        releaseType = "${curse_release}"
        changelogType = 'html'
        changelog = file('docs/changelog.html')
		
		addGameVersion '1.16.5'
		addGameVersion '1.16.4'
		addGameVersion '1.16.3'
		
		relations {
			requiredDependency 'curios'
			requiredDependency 'patchouli'
		}
		
        //addArtifact(sourcesJar)
        //addArtifact(apiJar)
    }
}



task updateResources() {
    // NO-OP
}

tasks.updateResources.dependsOn processResources

tasks.withType(JavaCompile) {
    options.failOnError(true)
}

jar.finalizedBy('reobfJar') 
//publish.dependsOn('reobfJar')

publishing {

	// TODO This needs some work

    publications {
        mavenJava(MavenPublication) {
            artifact jar
        }
    }
    repositories {
        maven {
            url "file:///${project.projectDir}/mcmodsrepo"
        }
    }
}