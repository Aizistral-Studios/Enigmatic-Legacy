buildscript {
    repositories {
		maven { url "file:///${project.projectDir}/deps/" }
        maven { url = 'https://files.minecraftforge.net/maven' }
        maven {
            name "Sponge"
            url "https://repo.spongepowered.org/repository/maven-public/"
            content { includeGroup "org.spongepowered" }
        }
        maven {
			name "Gradle Plugin Portal"
			url 'https://plugins.gradle.org/m2/'
			content { 
				includeGroup "gradle.plugin.com.matthewprenger"
				includeGroup "com.modrinth"
			}
		}
        maven {
			url = 'https://maven.parchmentmc.org/'
		}
        mavenCentral()
    }
    dependencies {
        classpath group: 'net.minecraftforge.gradle', name: 'ForgeGradle', version: '5.1.+', changing: true
		classpath 'gradle.plugin.com.matthewprenger:CurseGradle:1.4.0'
        classpath group: 'org.spongepowered', name: 'mixingradle', version: '0.7-1.0.0-SNAPSHOT'
		classpath 'org.parchmentmc:librarian:1.+'
    }
}

plugins {
    id "com.modrinth.minotaur" version "2.+"
}

println("Applying plugins...")

apply plugin: 'eclipse'
apply plugin: 'idea'
apply plugin: 'maven-publish'
apply plugin: 'net.minecraftforge.gradle'
apply plugin: 'org.parchmentmc.librarian.forgegradle'
apply plugin: 'com.matthewprenger.cursegradle'
apply plugin: 'org.spongepowered.mixin'
apply from: 'common.gradle'

subprojects {
    dep_forge     = rootProject.dep_forge
    dep_minecraft = rootProject.dep_minecraft
    version_mc    = rootProject.version_mc
    version_forge = rootProject.version_forge
    version_mcp   = rootProject.version_mcp
}

mixin {
	println("Specifying refmaps for Mixin...")
	
    add sourceSets.main, "enigmaticlegacy.refmap.json"
}

minecraft {
	println("Specifying Access Transformer configuration...")	
	accessTransformer = file('src/main/resources/META-INF/accesstransformer.cfg')
}

dependencies {
	println("Processing dependencies...")
   
	// Mixin
    annotationProcessor 'org.spongepowered:mixin:0.8.4:processor'
    annotationProcessor 'com.google.code.gson:gson:2.8.0'
    annotationProcessor 'com.google.guava:guava:21.0'
    annotationProcessor 'org.ow2.asm:asm-tree:7.2'
    annotationProcessor 'org.ow2.asm:asm-commons:7.2'
    annotationProcessor 'org.ow2.asm:asm-util:7.2'

    // implementation to run it locally

    // # --- JEI --- #
    implementation fg.deobf("mezz.jei:jei-${version_mc}-forge:${version_jei}")
    implementation fg.deobf("mezz.jei:jei-${version_mc}-common-api:${version_jei}")

    // # --- Curios --- #
    implementation fg.deobf("top.theillusivec4.curios:curios-forge:${version_mc}-${version_curios}")
    implementation fg.deobf("top.theillusivec4.curios:curios-forge:${version_mc}-${version_curios}:api")

    // # --- Patchouli --- #
    implementation fg.deobf("vazkii.patchouli:Patchouli:${version_mc}-${version_patchouli}")
    implementation fg.deobf("vazkii.patchouli:Patchouli:${version_mc}-${version_patchouli}:api")

    // # --- Caelus API --- #
    implementation fg.deobf("top.theillusivec4.caelus:caelus-forge:${version_mc}-${version_caelus}")

    // TODO :: Is this needed?
    implementation fg.deobf("curse.maven:world-name-randomizer-348277:4074642") // 1.19.2 - 1.5.0

    // # --- Apotheosis & dependencies --- #
    implementation fg.deobf('curse.maven:apotheosis-313970:4547399') // 1.19.2 - 6.2.1
    // Only needed to run it locally
    implementation fg.deobf("curse.maven:placebo-283644:4546960") // 1.19.2 - 7.2.0
}

tasks.withType(Jar) {
    manifest {
        attributes([
            "MixinConnector": "${project.group}.MixinConnector"
        ])
    }
}

processResources {
	doFirst {
		println("Processing resources...")
		println("Specifying The Acknowledgment version in book.json...")	
	}
	
	filesMatching('data/enigmaticlegacy/patchouli_books/the_acknowledgment/book.json') {
        filter {
			// Bold of you to assume I know how to inflate with Gradle
            it.replaceAll("~the_acknowledgment_edition", findProperty('the_acknowledgment_edition') ?: 'invalid_edition')
        }
    }
	
	exclude 'META-INF/mods.toml'
	
    from("${project.projectDir}/docs/META-INF") {
		println("Inflating mods.toml...")	
	
		// Replace properties in mods.toml with ones derived from settings.gradle
        include 'mods.toml'
        expand 'mod_license': mod_license, 
		'mod_version': project.version, 
		'mod_id': mod_id, 
		'mod_name': mod_name, 
		'mod_url': mod_url, 
		'mod_author': mod_author, 
		'mod_description': mod_description, 
		'mod_icon': mod_icon, 
		'issue_tracker_url': issue_tracker_url, 
		'mod_credits': mod_credits, 
		'dep_forge': dep_forge, 
		'dep_minecraft': dep_minecraft, 
		'dep_patchouli': dep_patchouli, 
		'dep_curios': dep_curios,
		'dep_caelus': dep_caelus
		rename 'mods.toml', 'META-INF/mods.toml'
    }

    from("${project.projectDir}/docs") {
        include 'changelog.html'
    }

    from("${project.projectDir}") {
       include 'LICENSE.md'
    }
}

task updateResources() {
    // NO-OP
}

tasks.updateResources.dependsOn processResources